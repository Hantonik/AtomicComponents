buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
    }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'idea'
apply plugin: 'maven-publish'

file('gradle.properties').withReader {
    def prop = new Properties()
    prop.load(it)
    project.ext.config = new ConfigSlurper().parse prop
}

repositories {
    maven { url 'https://hantonikdev.jfrog.io/artifactory/mods' }
    maven { url 'https://dvs1.progwml6.com/files/maven' }
    maven { url 'https://www.cursemaven.com' }
}

version = "${config.mod_version}"
group = "hantonik.${config.mod_id}"
archivesBaseName = "${config.mod_name}-${config.mc_version}"

sourceSets.main.resources {
    srcDir 'src/generated/resources'

    exclude '.cache'
}

java.toolchain.languageVersion = JavaLanguageVersion.of(8)

println "Starting build of ${config.mod_name}, Version: ${config.mod_version}"
println "Using Forge: ${config.forge_version} and Core: ${config.core_version}, for Minecraft: ${config.mc_version}, with Mappings: ${config.mappings_version}-${config.mappings_channel}"

minecraft {
    mappings channel: "${config.mappings_channel}", version: "${config.mappings_version}"

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            mods {
                atomiccomponents {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            mods {
                atomiccomponents {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            args '--mod', 'atomiccomponents', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')

            mods {
                atomiccomponents {
                    source sourceSets.main
                }
            }
        }
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${config.mc_version}-${config.forge_version}"

    runtimeOnly fg.deobf("mezz.jei:jei-1.16.5:7.7.1.129")
    runtimeOnly fg.deobf("curse.maven:jade-324717:3467445")

    implementation fg.deobf("hantonik.atomiccore:AtomicCore-${config.mc_version}:${config.core_version}")
}

task replaceResources(type: Copy) {
    outputs.upToDateWhen { false }

    from(sourceSets.main.resources.srcDirs) {
        include 'META-INF/mods.toml'

        expand  'mod_version': "$version",
                'mod_name': "${config.mod_name}",
                'core_version': "${config.supported_core_versions}"
    }

    into "$buildDir/resources/main/"
}

processResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE

    from(sourceSets.main.resources.srcDirs) {
        exclude 'META-INF/mods.toml'
    }

    finalizedBy replaceResources
}

jar {
    manifest {
        attributes([
                "Specification-Title": archivesBaseName,
                "Specification-Vendor": "Hantonik",
                "Specification-Version": "1",
                "Implementation-Title": project.name,
                "Implementation-Version": version,
                "Implementation-Vendor" :"Hantonik",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

jar.finalizedBy('reobfJar')

task sourcesJar(type: Jar) {
    classifier = "sources"
    from sourceSets.main.allJava
}

artifacts {
    archives jar
    archives sourcesJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
            artifact sourcesJar

            artifactId "$archivesBaseName"
        }
    }
    repositories {
        maven {
            url "https://hantonikdev.jfrog.io/artifactory/mods/"

            credentials {
                username System.getenv("REPO_USER")
                password System.getenv("REPO_PASSWORD")
            }
        }
    }
}
